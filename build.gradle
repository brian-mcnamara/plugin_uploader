plugins {
    id 'java'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.12.0"
}

group 'dev.bmac.intellij.plugins'
version '1.3.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation "javax.xml.bind:jaxb-api:2.3.1"
    implementation 'com.sun.xml.bind:jaxb-impl:2.3.3-b02'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation "com.github.rholder:guava-retrying:2.0.0"
    implementation "com.amazonaws:aws-java-sdk-s3:1.12.220"
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.3'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.3.1'
    testImplementation('com.adobe.testing:s3mock-junit4:2.4.10') {
        exclude group: 'org.slf4j', module: '*'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
}

def generatedResourcesDir = file("$buildDir/generated-resources")
processResources {
    def outputFile = file("$generatedResourcesDir/project.version")
    outputs.dir(generatedResourcesDir)
    inputs.property("version", project.version)
    doFirst {
        outputFile.write(project.version as String)
    }
}

sourceSets {
    main {
        resources {
            srcDirs generatedResourcesDir
        }
    }
}

gradlePlugin {
    plugins {
        pluginUploader {
            id = 'dev.bmac.intellij.plugin-uploader'
            implementationClass = 'dev.bmac.gradle.intellij.IntellijPublishPlugin'
        }
    }
}

pluginBundle {
    description = 'Manages uploading and updating updatePlugins.xml for IntelliJ plugins to a private repository'
    vcsUrl = 'https://github.com/brian-mcnamara/plugin_uploader'
    website = 'https://github.com/brian-mcnamara/plugin_uploader'
    tags = ['IntelliJ', "plugins", "updatePlugins.xml"]

    plugins {
        pluginUploader {
            displayName = 'IntelliJ plugin publisher'
        }
    }
}